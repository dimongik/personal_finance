//#Если Сервер
#Область ОбработчикиСобытий
// Код процедур и функций

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
		
	Если Проведен И РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		Отказ = Истина; //Повторно не проводим
	КонецЕсли;

КонецПроцедуры

//
// Обработка проведения.
// 
// Параметры:
//  Отказ - Булево - Отказ
//  РежимПроведения - РежимПроведенияДокумента - Режим проведения
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.Бюджет.Записывать = Истина;
	Движения.Бюджет.Очистить();
		
	Если ВидОтражения = Перечисления.ВидыОтраженийБюджета.План Тогда
		//Сторнируем данные за этот месяц по данной статье затрат
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.Бюджет");
		ЭлементБлокировки.УстановитьЗначение("СтатьяЗатрат", СтатьяЗатрат);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	БюджетОбороты.СтатьяЗатрат,
			|	БюджетОбороты.ПланОборот КАК План
			|ИЗ
			|	РегистрНакопления.Бюджет.Обороты(&НачалоПериода, &КонецПериода,, СтатьяЗатрат = &СтатьяЗатрат) КАК БюджетОбороты";
		
		Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Дата));
		Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Дата));
		Запрос.УстановитьПараметр("СтатьяЗатрат", СтатьяЗатрат);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			Движение = Движения.Бюджет.Добавить();
			Движение.Период = Дата;
			Движение.План = - Выборка.План;
			Движение.СтатьяЗатрат = СтатьяЗатрат;
		КонецЦикла;
		
		Движение = Движения.Бюджет.Добавить();
		Движение.Период = Дата;
		Движение.План = Сумма;
		Движение.СтатьяЗатрат = СтатьяЗатрат;
		
	КонецЕсли;	
	
	Если ВидОтражения = Перечисления.ВидыОтраженийБюджета.Факт Тогда
		Движение = Движения.Бюджет.Добавить();
		Движение.Период = Дата;
		Движение.Факт 	= Сумма;
		Движение.СтатьяЗатрат = СтатьяЗатрат;	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти